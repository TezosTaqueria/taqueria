export namespace IncDec {
  export type storage = int;
  type ret = [list<operation>, storage];
  
  // Three entrypoints

  @entry
  const increment = (delta: int, store: storage): ret =>
    [list([]), store + delta];
  @entry
  const decrement = (delta: int, store: storage): ret =>
    [list([]), store - delta];
  @entry
  const reset = (_: unit, _s: storage): ret => [list([]), 0]
};

/* Tests for main access point */

const test_initial_storage =
  (
    () => {
      let initial_storage = 42;
      let contract = Test.Next.Originate.contract(contract_of (IncDec), initial_storage, 0 as tez);
      return Assert.assert(Test.Next.Typed_address.get_storage(contract.taddr) == initial_storage)
    }
  )();

const test_increment =
  (
    () => {
      let initial_storage = 42;
      let contract  = Test.Next.Originate.contract(contract_of (IncDec), initial_storage, 0 as tez);
      Test.Next.Typed_address.transfer_exn(contract.taddr, (Increment (1)), 1mutez);
      return Assert.assert(Test.Next.Typed_address.get_storage(contract.taddr) == initial_storage + 1)
    }
  )();